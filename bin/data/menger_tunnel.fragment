uniform vec3 iResolution;
uniform vec4 iMouse;
uniform float iGlobalTime;
uniform sampler2DRect iCh;
uniform sampler2DRect iCh1;

const int MAX_RAY_STEPS = 180;
const float RAY_STOP_TRESHOLD = 0.0005;
const int MENGER_ITERATIONS = 5;

vec3 rotateX(float a, vec3 v) {   return vec3(v.x, cos(a) * v.y + sin(a) * v.z, cos(a) * v.z - sin(a) * v.y); }
vec3 rotateY(float a, vec3 v) { return vec3(cos(a) * v.x + sin(a) * v.z, v.y, cos(a) * v.z - sin(a) * v.x); }
vec2 rotate(float a, vec2 v) { return vec2(cos(a) * v.x + sin(a) * v.y,cos(a) * v.y - sin(a) * v.x); }

float maxcomp(vec2 v) { return max(v.x, v.y); }

float sdCross(vec3 p) {
	p = abs(p);
	vec3 d = vec3(max(p.x, p.y), max(p.y, p.z),  max(p.z, p.x));
	return min(d.x, min(d.y, d.z)) - (1.0 / 3.0);
}

float sdCrossRep(vec3 p) {
	vec3 q = mod(p + 1.0, 2.0) - 1.0;
	return sdCross(q);
}

float sdCrossRepScale(vec3 p, float s) {
	return sdCrossRep(p * s) / s;	
}

float scene(vec3 p) {
	float scale = 1.0;
	float dist = 0.0;
	for (int i = 0; i < MENGER_ITERATIONS; i++) {
		dist = max(dist, -sdCrossRepScale(p, scale));
		scale *= 3.0;
	}
	return dist;
}

vec3 hsv2rgb(vec3 c)
{
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

vec4 colorize(float c) {
	float hue = mix(1.6, .5, min(c * .2 - 0.05, 1.0));
	float sat = .0 - pow(c, 1.0);
	float lum = c;
	vec3 hsv = vec3(hue, sat, lum);
	vec3 rgb = hsv2rgb(hsv);
	return vec4(rgb, 1.0);	
}

void main() {
	vec2 screenPos = gl_FragCoord.xy / iResolution.xy * 2.0 - 1.0;
	vec2 mousePos = iMouse.xy / iResolution.xy * 2.0 - 1.0;
	
	vec3 cp = rotateX(iMouse.x, vec3(iMouse.x, iMouse.y, iMouse.x));

	//vec3 cameraPos = vec3(0.16 * sin(iGlobalTime), 0.16 * cos(iGlobalTime), iGlobalTime);
	vec3 cameraPos = vec3(iMouse.xyz);
	vec3 cameraDir = vec3(0.0, 0.0, 1.0);
	vec3 cameraPlaneU = vec3(1.0, 0.0, 0.0);
	vec3 cameraPlaneV = vec3(0.0, 1.0, 0.0) * (iResolution.y / iResolution.x);

	vec3 rayPos = cameraPos;
	vec3 rayDir = cameraDir + screenPos.x * cameraPlaneU + screenPos.y * cameraPlaneV;
	rayDir = normalize(rayDir);
	
	float dist = scene(rayPos);
	int stepsTaken;
	for (int i = 0; i < MAX_RAY_STEPS; i++) {
		if (dist < RAY_STOP_TRESHOLD) {
			continue;
		}
		rayPos += rayDir * dist;
		dist = scene(rayPos);
		stepsTaken = i;
	}
	vec4 color = colorize(pow(float(stepsTaken) / float(MAX_RAY_STEPS), 0.9));
	gl_FragColor = color;
}
