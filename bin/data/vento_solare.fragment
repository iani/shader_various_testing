#extension GL_ARB_texture_rectangle : enable

uniform vec3 iResolution;
uniform float iGlobalTime;
uniform sampler2DRect iChannel0;
uniform sampler2DRect iChannel1;
uniform vec4 iMouse;

const float sphsize=1.3; // planet size
const float dist=.27; // distance for glow and distortion
const float perturb=.2; // distortion amount of the flow around the planet
const float displacement=.008; // hot air effect
const float windspeed=.6; // speed of wind flow
const float steps=100.; // number of steps for the volumetric rendering
const float stepsize=.015; 
const float brightness=.43;
const vec3 planetcolor=vec3(0.55,0.4,0.3);
const float fade=.005; //fade by distance
const float glow=3.5; // glow amount, mainly on hit side

const int iterations=13; 
const float fractparam=.7;
const vec3 offset=vec3(1.5,2.,-1.5);


float wind(vec3 p) {
	float d=max(0.,dist-max(0.,length(p)-sphsize)/sphsize)/dist; // for distortion and glow area
	float x=max(0.2,p.x*2.); // to increase glow on left side
	p.y*=1.+max(0.,-p.x-sphsize*.25)*1.5; // left side distortion (cheesy)
	p-=d*normalize(p)*perturb; // spheric distortion of flow
	p+=vec3(iGlobalTime*windspeed,0.,0.); // flow movement
	p=abs(fract((p+offset)*.1)-.5); // tile folding 
	for (int i=0; i<iterations; i++) {  
		p=abs(p)/dot(p,p)-fractparam; // the magic formula for the hot flow
	}
	return length(p)*(1.+d*glow*x)+d*glow*x; // return the result with glow applied
}

void main(void) {
	vec2 uv = gl_FragCoord.xy / iResolution.xy-.5;
	vec3 dir=vec3(uv,1.);
	dir.x*=iResolution.x/iResolution.y;
	vec3 from=vec3(0.,0.,-2.+texture2DRect(iChannel0,uv*.5+iGlobalTime).x*stepsize);
	float v=0., l=-0.0001, t=iGlobalTime*windspeed*.2;
	for (float r=10.;r<steps;r++) {
		vec3 p=from+r*dir*stepsize;
		float tx=texture2DRect(iChannel0,uv*.2+vec2(t,0.)).x*displacement;
		//if (length(p)-sphsize-tx>0.)
		v+=min(50.,wind(p))*max(0.,1.-r*fade); 
		//else if (l<0.) 
		l=pow(max(.53,dot(normalize(p),normalize(vec3(-1.,.5,-0.3)))),4.)*(.5+texture2DRect(iChannel1,gl_FragCoord.xy+vec2(tx+t*.5,0.) ).x*2.);
	}
////uv*vec2(2.,1.)*(1.+p.z*.5)+vec2(tx+t*.5,0.)
	v/=steps; v*=brightness;
	vec3 col=vec3(v*1.25,v*v,v*v*v)+l*planetcolor;
	col*=1.-length(pow(abs(uv),vec2(5.)))*14.;
	gl_FragColor = vec4(col,1.0);
}
